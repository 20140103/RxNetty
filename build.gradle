// Establish version and status
ext.githubProjectName = rootProject.name // Change if github project name is not the same as the root project's name
group = "com.netflix.${githubProjectName}"

buildscript {
    repositories {
        mavenLocal()
        mavenCentral() // maven { url 'http://jcenter.bintray.com' }
    }
    apply from: file('gradle/buildscript.gradle'), to: buildscript
}

allprojects {
    repositories {
        mavenLocal()
        mavenCentral() // maven { url: 'http://jcenter.bintray.com' }
    }
}

apply from: file('gradle/convention.gradle')
apply from: file('gradle/maven.gradle')
apply from: file('gradle/license.gradle')
apply from: file('gradle/release.gradle')

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'osgi'
apply plugin: 'groovy'

sourceCompatibility = JavaVersion.VERSION_1_6
targetCompatibility = JavaVersion.VERSION_1_6

group = "com.netflix.rxnetty"

// make 'examples' use the same classpath
configurations {
    // add new configuration
    examples

    //adding a configuration that extends existing configuration:
    //(testCompile was added by the java plugin)
    examples.extendsFrom(compile)
    examplesCompile.extendsFrom(compile)
    examplesRuntime.extendsFrom runtime
}

sourceSets.test.java.srcDir 'src/main/java'

tasks.withType(Javadoc).each {
    it.classpath = sourceSets.main.compileClasspath
}

//include /src/examples folder
sourceSets {
    examples {
        // no idea why this is needed
        java.srcDirs = []
    }
}

//include 'examples' in build task
tasks.build { dependsOn(examplesClasses) }

eclipse {
    classpath {
        // include 'provided' dependencies on the classpath
        plusConfigurations += configurations.provided
        plusConfigurations += configurations.examples

        downloadSources = true
        downloadJavadoc = true
    }
}

idea {
    module {
        // include 'provided' dependencies on the classpath
        scopes.PROVIDED.plus += configurations.provided
        scopes.PROVIDED.plus += configurations.provided
        scopes.COMPILE.plus += configurations.examples
        scopes.RUNTIME.plus += configurations.examplesRuntime
    }
}

dependencies {
    compile 'io.netty:netty-handler:4.0.14.Final'
    compile 'io.netty:netty-codec-http:4.0.14.Final'
    compile 'com.netflix.rxjava:rxjava-core:[0.14,)'
    // we only support Groovy in the /src/examples/ code
    examples 'org.codehaus.groovy:groovy-all:[2.1,)'
    examplesRuntime 'com.netflix.rxjava:rxjava-groovy:[0.14,)'
    testCompile 'junit:junit:4.10'
}


apply plugin: 'osgi'
apply plugin: 'groovy'



tasks.compileExamplesGroovy {
    classpath = classpath + files(compileGroovy.destinationDir) + (configurations.compile + configurations.examples)
}

jar {
    manifest {
        name = 'rx-netty'
        instruction 'Bundle-Vendor', 'Netflix'
        instruction 'Bundle-DocURL', 'https://github.com/Netflix/RxNetty'
        instruction 'Import-Package', '!org.junit,!junit.framework,!org.mockito.*,*'
    }
}
